
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтоПолныеПрава = РольДоступна("ПолныеПрава");
	Элементы.Домен.Доступность = ЭтоПолныеПрава;
	Элементы.Домен.ТолькоПросмотр = Не ЭтоПолныеПрава;
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	//Элементы.Родитель.Доступность = Сервер.ЕстьПрава("ПолныеПрава") Или Сервер.ЕстьПрава("Кадровик");
	УстановитьНадписьПользователяИБ();
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИсторию(Команда)
	ОткрытьФорму("РегистрСведений.УчетРабочегоВремени.Форма.ФормаИсторияЗаПериод",Новый Структура("ОтборПоПользователю",Объект.Ссылка));
КонецПроцедуры

&НаКлиенте
Процедура ПользовательИБНажатие(Элемент)
	Если Сервер.ЕстьПрава("ПолныеПрава") Тогда 
		УказатьПользователя();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура УказатьПользователя()
	СписокПольз = ПолучитьСписокПользователейИБ(Истина);
	//ВыбЭл = СписокПольз.ВыбратьЭлемент("Выберите пользователя ИБ");
	СписокПольз.ПоказатьВыборЭлемента(Новый ОписаниеОповещения("УказатьПользователяЗавершение",ЭтаФорма),"Выберите пользователя ИБ");
КонецПроцедуры

&НаКлиенте
Процедура УказатьПользователяЗавершение(ВыбЭл,ДопПараметры) Экспорт 
	Если ВыбЭл = Неопределено Тогда 
		Сообщить("Пользователь ИБ не выбран!");
	Иначе
		УстановитьПользователяИБ(ВыбЭл.Значение);
	КонецЕсли;	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСписокПользователейИБ(СПустым = Ложь)
    СписокПольз = Новый СписокЗначений;
	Если СПустым Тогда 
		СписокПольз.Добавить(Новый УникальныйИдентификатор("00000000-0000-0000-0000-000000000000"),"<пустой>");
	КонецЕсли;
	МасПольз = ПользователиИнформационнойБазы.ПолучитьПользователей();
	Для Каждого ЭлМас Из МасПольз Цикл 
		СписокПольз.Добавить(ЭлМас.УникальныйИдентификатор,ЭлМас.Имя);
	КонецЦикла;
	Возврат СписокПольз;
КонецФункции

&НаСервере 
Процедура УстановитьПользователяИБ(Ид)
	ПользИБ = Сервер.НайтиПользователяИБ(Ид);
	Если ПользИБ <> Неопределено Тогда 
		Объект.ПользовательИБ = ПользИБ.УникальныйИдентификатор;
		УстановитьНадписьПользователяИБ(ПользИБ.Имя);
	КонецЕсли;
КонецПроцедуры

&НаСервере 
Процедура УстановитьНадписьПользователяИБ(Имя = "")
	Если Имя = "" Тогда 
		Ид = Объект.ПользовательИБ;
		ПользИБ = Сервер.НайтиПользователяИБ(Ид);
		Если ПользИБ <> Неопределено Тогда 
			Имя = ПользИБ.Имя;
		Иначе
			Имя = "УКАЗАТЬ";
		КонецЕсли;
	КонецЕсли;	
	Элементы.НадписьПользовательИБ.Заголовок = Имя;
КонецПроцедуры

&НаКлиенте
Процедура РодительПриИзменении(Элемент)
	Объект.Родитель1 = Объект.Родитель;
КонецПроцедуры
