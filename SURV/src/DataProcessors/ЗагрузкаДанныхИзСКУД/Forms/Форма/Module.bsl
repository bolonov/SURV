
&НаКлиенте
Процедура ЗагрузитьСобытия(Команда)
	Если Не ЗначениеЗаполнено(Объект.Офис) Тогда 
		ПоказатьПредупреждение(Неопределено, "Не выбран Офис!");
		Возврат;
	КонецЕсли;
	ЗагрузкаСобытий();
	Элементы.События.Обновить();
	ПоказатьПредупреждение(Неопределено, "Готово!",2);
КонецПроцедуры

&НаСервере
Процедура ЗагрузкаСобытий()
	
	СтруктураПараметров = Новый Структура;
	Если ЗначениеЗаполнено(Объект.ДатаНачала) Тогда 
		СтруктураПараметров.Вставить("ДатаНачала",Объект.ДатаНачала);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.ДатаОкончания) Тогда 
		СтруктураПараметров.Вставить("ДатаОкончания",Объект.ДатаОкончания);
	КонецЕсли;
	Если ЗначениеЗаполнено(Объект.Пользователь) Тогда 
		СтруктураПараметров.Вставить("Почта",Объект.Пользователь.Email);
	КонецЕсли;
	
	Объект.События.Очистить();
	ТаблицаСобытий = Сервер.ПолучитьТаблицуСобытийИзСКУД(Объект.Офис,СтруктураПараметров);	
	Если ТаблицаСобытий <> Неопределено Тогда 
		Для Каждого СтрокаТабл Из ТаблицаСобытий Цикл 
			НовСтрокаТЧ = Объект.События.Добавить();
			ЗаполнитьЗначенияСвойств(НовСтрокаТЧ,СтрокаТабл);
			НовСтрокаТЧ.Офис = Объект.Офис;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВРегистр(Команда)
	Если Объект.События.Количество() > 0 Тогда 
		ТекстВопроса = "ВНИМАНИЕ! За "+?(ЗначениеЗаполнено(Объект.ДатаНачала) Или ЗначениеЗаполнено(Объект.ДатаОкончания),"период ","весь период работы ") +
						?(ЗначениеЗаполнено(Объект.ДатаНачала),"с "+Формат(Объект.ДатаНачала,"ДФ=дд.ММ.гггг ЧЧ:мм:сс")+" ","") +
						?(ЗначениеЗаполнено(Объект.ДатаОкончания),"по "+Формат(Объект.ДатаОкончания,"ДФ=дд.ММ.гггг ЧЧ:мм:сс")+" ","") + "
						|" + ?(ЗначениеЗаполнено(Объект.Пользователь),"по пользователю "+СокрЛП(Объект.Пользователь)+" ","по всем пользователям офиса "+СокрЛП(Объект.Офис)+" ") + "
						|будут замещены все записи входов/выходов по карточкам!
						|Вы уверены, что хотите продолжить?";
		ПоказатьВопрос(Новый ОписаниеОповещения("ЗаписатьВРегистрЗавершение", ЭтотОбъект), ТекстВопроса,РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ЗаписатьВРегистрЗавершение(РезультатВопроса, ДополнительныеПараметры) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Нет Тогда 
		Возврат;
	КонецЕсли;
	ОчиститьСтарыеСобытия();
	ЗаписатьСобытияВРегистр();
	ПоказатьПредупреждение(Неопределено, "Готово!",2);

КонецПроцедуры

&НаСервере
Процедура ОчиститьСтарыеСобытия()
	НачатьТранзакцию();
	Запрос = Новый Запрос("ВЫБРАТЬ
	                      |	УчетРабочегоВремени.Период,
	                      |	УчетРабочегоВремени.Пользователь,
	                      |	УчетРабочегоВремени.ВидВремени,
	                      |	УчетРабочегоВремени.ВремяОкончания,
	                      |	УчетРабочегоВремени.Дверь,
	                      |	УчетРабочегоВремени.Комментарий,
	                      |	УчетРабочегоВремени.Офис
	                      |ИЗ
	                      |	РегистрСведений.УчетРабочегоВремени КАК УчетРабочегоВремени
	                      |ГДЕ
	                      |	УчетРабочегоВремени.Период МЕЖДУ &Дата1 И &Дата2
	                      |	И УчетРабочегоВремени.ВидВремени = &ВидВремени
						  |	И УчетРабочегоВремени.Офис = &Офис
	                      |	И (УчетРабочегоВремени.Пользователь = &Пользователь
	                      |			ИЛИ &Пользователь = ЗНАЧЕНИЕ(Справочник.Пользователи.ПустаяСсылка))");
	Запрос.УстановитьПараметр("Дата1",Объект.ДатаНачала);
	Запрос.УстановитьПараметр("Дата2",?(ЗначениеЗаполнено(Объект.ДатаОкончания),Объект.ДатаОкончания,'39990101'));
	Запрос.УстановитьПараметр("ВидВремени",Перечисления.ВидыВремени.ЗарегистрированоПоКарте);
	Запрос.УстановитьПараметр("Офис",Объект.Офис);
	Запрос.УстановитьПараметр("Пользователь",Объект.Пользователь);
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл 
		НабЗап = РегистрыСведений.УчетРабочегоВремени.СоздатьНаборЗаписей();
		НабЗап.Отбор.Период.Установить(Выборка.Период,Истина);
		НабЗап.Отбор.Пользователь.Установить(Выборка.Пользователь,Истина);
		НабЗап.Записать(Истина);
	КонецЦикла;
	ЗафиксироватьТранзакцию();
КонецПроцедуры

&НаСервере
Процедура ЗаписатьСобытияВРегистр()
	Сервер.ЗагрузитьСобытияВРегистр(Объект.События,Истина);	
КонецПроцедуры

&НаКлиенте
Процедура ДатаОкончанияОбработкаВыбора(Элемент, ВыбранноеЗначение, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(ВыбранноеЗначение) Тогда 
		ВыбранноеЗначение = КонецДня(ВыбранноеЗначение);
	КонецЕсли;
КонецПроцедуры
